buildscript {
    repositories {
        jcenter()
        maven { url "http://files.minecraftforge.net/maven" }
        maven { url "http://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

repositories {
    maven { url "http://repo.spongepowered.org/maven" }
    maven { url "http://www.dimdev.org/maven" }
    ivy {
        url "https://google.com/404"
        ivyPattern("https://google.com/404") // Workaround for https://github.com/gradle/gradle/issues/4107
        artifactPattern("https://media.forgecdn.net/files/[organisation]/[module]/[revision].[ext]")
    }
}

dependencies {
    implementation("org.dimdev:mixin:0.7.11-SNAPSHOT") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    compileOnly "2555:531:BiomesOPlenty-1.12.2-7.0.1.2384-universal@jar"
    compileOnly "2561:393:twilightforest-1.12.2-3.7.424-universal@jar"
    compileOnly "2569:273:Thaumcraft-1.12.2-6.1.BETA16@jar"
    compileOnly "2572:684:AbyssalCraft-1.12.2-1.9.4.10@jar"
    compileOnly "2509:208:Creeping+Nether-2.0@jar"
    compileOnly "2497:541:GeographiCraft-1.12-0.8.9b@jar"
    compileOnly "2581:550:mystcraft-1.12.2-0.13.4.05@jar"
    compileOnly "2575:801:AdvancedRocketry-1.12.2-1.4.0-88-universal@jar"
    compileOnly "2460:570:worldedit-forge-mc1.12-6.1.8-dist@jar"
    compileOnly "2498:312:journeymap-1.12.2-5.5.2@jar"
}

def travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
def versionSuffix = travisBuildNumber != null ? travisBuildNumber : "SNAPSHOT"

version "1.0.1-$versionSuffix"
group "org.dimdev.jeid"
archivesBaseName = "JustEnoughIDs"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version "1.12.2-14.23.4.2703"
    runDir "run"
    mappings "stable_39"
    makeObfSourceJar false

    def args = [
            "-Dfml.coreMods.load=org.dimdev.jeid.JEIDLoadingPlugin",
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true"
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

mixin {
    add sourceSets.main, "mixins.jeid.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

processResources {
    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // Re-run this task when these change
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
}

jar {
    classifier "nolibs"
    manifest.attributes(
            "FMLCorePluginContainsFMLMod": "true",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "FMLCorePlugin": "org.dimdev.jeid.JEIDLoadingPlugin",
            "ForceLoadAsMod": "true"
    )
}

shadowJar {
    classifier ""
    exclude "LICENSE.txt", "dummyThing"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}

// These are broken
runClient.group "other"
runServer.group "other"
